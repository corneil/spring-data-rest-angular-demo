buildscript {
    ext {
        springBootVersion = '1.5.3.RELEASE'
        springPlatformVersion = 'Athens-SR5'
        manifestVendor = 'com.github.corneil'
        angularVersion = '1.6.4'
    }
    repositories {
        mavenLocal()
        jcenter()
        mavenCentral()
    }
}

plugins {
    id 'org.springframework.boot' version '1.5.3.RELEASE'
}

group 'com.github.corneil'
apply plugin: 'eclipse'
apply plugin: 'idea'

subprojects {
    version = '0.0.1-SNAPSHOT'
    repositories {
        mavenLocal()
        jcenter()
        mavenCentral()
    }
    apply from: "$rootProject.projectDir/dependencies.gradle"
}

eclipse {
    classpath {
        containers.remove 'org.eclipse.jdt.launching.JRE_CONTAINER'
        containers 'org.eclipse.jdt.launching.JRE_CONTAINER/org.eclipse.jdt.internal.debug.ui.launcher.StandardVMType/JavaSE-1.8'
    }
}

// Thanks to http://mrhaki.blogspot.co.za/2016/03/gradle-goodness-enable-compiler.html
idea {
    project {
        ipr {
            withXml { provider ->
                // Get XML as groovy.util.Node to work with.
                def projectXml = provider.asNode()
                // Find compiler configuration component.
                def compilerConfiguration = projectXml.component.find { component ->
                    component.'@name' == 'CompilerConfiguration'
                }
                // Replace current annotationProcessing
                // that is part of the compiler configuration.
                def currentAnnotationProcessing = compilerConfiguration.annotationProcessing
                currentAnnotationProcessing.replaceNode {
                    annotationProcessing {
                        profile(name: 'Default', default: true, enabled: true) {
                            processorPath(useClasspath: true)
                        }
                    }
                }
            }
        }
    }
}

